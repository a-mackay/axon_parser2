use std::str::FromStr;
use crate::ast::{Arg, Args, Assign, Def, Expr, ExprOrStmt, FnCall, Id, Lambda, Lines, Lit, Param, Stmt, TermBase};

grammar;

pub Assign: Assign = {
    <id:Id> "=" <e:Expr> => Assign::new(id, e),
}

pub Def: Def = {
    <id:Id> ":" Nl? <e:Expr> => Def::new(id, e),
}

pub Do: Lines = {
    "do" Nl? <Lines> "end" => <>,
}

pub Expr: Expr = {
    Do => Expr::Do(<>),
    Id => Expr::Id(<>),
    Lambda => Expr::Lambda(<>),
    Lit => Expr::Lit(<>),
}

pub Lines: Lines = {
    <(<ExprOrStmt> ";"? Nl?)+> => Lines::new(<>),
}

pub Lambda: Lambda = {
    "(" ")" "=>" <ExprOrStmt> => Lambda::new_no_args(Lines::new_from_eos(<>)),
    <id:Id> "=>" <eos:ExprOrStmt> => {
        let param = Param::new(id);
        Lambda::new(vec![param], Lines::new_from_eos(eos))
    },
    "(" <first_param:Param> <params:("," <Param>)*> ")" "=>" <eos:ExprOrStmt> => {
        let mut params = params;
        params.insert(0, first_param);
        Lambda::new(params, Lines::new_from_eos(eos))
    },
}

pub Lit: Lit = {
    "null" => Lit::Null,
    "true" => Lit::Bool(true),
    "false" => Lit::Bool(false),
    Num => Lit::Num(<>),
}

pub Param: Param = {
    <id:Id> ":" <e:Expr> => Param::new_with_default_arg(id, e),
    Id => Param::new(<>),
}

pub Stmt: Stmt = {
    Assign => Stmt::Assign(<>),
    Def => Stmt::Def(<>),
    "return" <Expr> => Stmt::Return(<>),
    "throw" <Expr> => Stmt::Throw(<>),
}

pub TermBase: TermBase = {
    <id:Id> "(" ")" => {
        let fn_call = FnCall::new(id, Args::empty());
        TermBase::FnCall(fn_call)
    },
    <id:Id> "(" <first_expr:(Nl? <Expr>)> <exprs:("," Nl? <Expr>)*> Nl? ")" => {
        let mut exprs = exprs;
        exprs.insert(0, first_expr);
        let args = exprs.into_iter().map(Arg::new).collect();
        let fn_call = FnCall::new(id, Args::new(args));
        TermBase::FnCall(fn_call)
    },
    <Id> => TermBase::Var(<>),
    <Lit> => TermBase::Lit(<>),
}

ExprOrStmt: ExprOrStmt = {
    Expr => ExprOrStmt::Expr(<>),
    Stmt => ExprOrStmt::Stmt(<>),
}

Id: Id = {
    r"[a-z]([a-zA-Z0-9_])*" => Id::new(<>),
}

Num: f64 = {
    r"-?\d+(\.\d+)?" => f64::from_str(<>).unwrap(),
}

match {
    r"[[:space:]&&[^\n]]*" => { },
    r"\n" => Nl,
} else {
    _
}